#version 330 core
out vec4 FragColor;  
in vec3 normal;
in vec3 fragPos;

struct Material {
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
    float shininess;
};

struct Light {
    vec3 position;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

uniform Light light;
uniform Material material;
uniform vec3 cameraPos;

void main()
{
    vec3 ambient = light.ambient * material.ambient;

    vec3 norm = normalize(normal);
    vec3 lightDirection = normalize(light.position - fragPos);
    float diffuseStrength = max(dot(norm, lightDirection), 0.0f);
    vec3 diffuse = light.diffuse * (diffuseStrength * material.diffuse);

    vec3 reflectedLight = reflect(-lightDirection, norm);
    vec3 cameraDir = normalize(cameraPos - fragPos); 
    float spec = pow(max(dot(reflectedLight, cameraDir), 0.0f), material.shininess);
    vec3 specular = light.specular * material.specular * spec;

    vec3 fragColorRgb = ambient + diffuse + specular;
    FragColor = vec4(fragColorRgb, 1.0f);
}